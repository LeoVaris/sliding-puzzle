plugins {
    id 'java'
    id 'application'
    id 'jacoco'
    id 'checkstyle'
    id "com.github.johnrengelman.shadow" version "6.1.0"
}
apply plugin : "java"
ext {
    javaMainClass = "util.PermutationTester"
    testMainClass = "util.RandomizedTester"
}

if (project.hasProperty("args")) {
    ext.cmdargs = project.getProperty("args")
} else {
    ext.cmdargs = ""
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation 'junit:junit:4.12'
    testImplementation 'junit:junit:4.13'
}

test {
    testLogging.showStandardStreams = true
    systemProperties System.getProperties()
}

application {
    mainClassName = 'io.Main'
}

run {
    standardInput = System.in
}

jar {
    manifest {
        attributes 'Main-Class': 'io.Main'
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['io/**', 'util/**'])
        }))
    }
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compile
}

task runPermutationTest(type: JavaExec) {
    group = "Execution"
    description = "Performance tests"
    classpath = sourceSets.main.runtimeClasspath
    main = javaMainClass
}

task runRandomizedTest(type: JavaExec) {
    group = "Execution"
    description = "Performance tests"
    classpath = sourceSets.main.runtimeClasspath
    main = testMainClass
    args cmdargs.split()
}
